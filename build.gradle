plugins {
    id 'groovy'
    id "jacoco"
    id "ru.vyarus.quality" version "4.6.0"
    id 'maven-publish'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id "com.diffplug.spotless-changelog" version "2.2.0"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

apply from: 'gradle/publishing.gradle'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.approvaltests:approvaltests:15.4.0'
    implementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.6.0'
    testImplementation 'dk.bitcraft:LogCollector:0.9.0'
    testRuntimeOnly 'com.google.code.gson:gson:2.9.0'
}

group = 'com.github.rzabini'

test {
    useJUnitPlatform()
}

scmVersion {
    ignoreUncommittedChanges = false
    localOnly = false

    tag {
        initialVersion = { tag, position ->
            spotlessChangelog.versionNext
        }
    }

    hooks {
        pre { context ->
            assert context.releaseVersion != context.previousVersion: "you must update changelog to release"
            assert spotlessChangelog.versionNext == spotlessChangelog.versionLast: "changelog has unreleased changes"
        }
    }

    versionIncrementer { context ->
        context.currentVersion.valueOf(spotlessChangelog.versionNext)
    }
}

version = scmVersion.version